import static org.stackwork.gradle.docker.ModuleType.*

buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'nebula.info' version '2.2.4'
  id 'nebula.contacts' version '2.2.0'
  id 'nebula.maven-apache-license' version '4.0.1'

  id 'nebula.nebula-release' version '3.0.2'

  id 'org.stackwork' version '0.6.0'
}

contacts {
  'qkrijger@gmail.com' {
    moniker 'Quinten Krijger'
    github 'krijger'
  }
  'ernst.naezer@gmail.com' {
    moniker 'Ernst Naezer'
    github 'ernstnaezer'
  }
}

def travisBuild = System.getenv('TRAVIS_BRANCH') != null
def travisRelease = project.hasProperty('release.travisci') && project.property('release.travisci').toBoolean()
logger.info "Running on Travis: '$travisBuild'"
logger.info "Performing release on Travis: '$travisRelease'"

allprojects {
  stackwork {
    stopContainers = !travisBuild
  }
}

stackwork {
  imageName = 'qkrijger/hello-world-wiremock-server'
  moduleType = DELIVERABLE_IMAGE
}

tasks.tagImage.doFirst {
  // to locally build and tag an image, the elaborate version with git hash etc must have the plus-sign removed, for
  // that is not allowed in an image name
  project.version = project.version.toString().replaceAll('\\+', '')
}
tasks.release.dependsOn tasks.pushImage
tasks.pushImage.onlyIf { travisRelease }
